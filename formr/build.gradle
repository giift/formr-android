apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

def sdkVersion;

def getVersionNameTimestamp() {
    return new Date().format('yyyy.MM.ddHHmmss')
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 1
        versionName getVersionNameTimestamp()
        sdkVersion = versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    jacoco {
        version = '0.7.4.201502262128'
    }
}

configurations {
    deployerJars
}

repositories {
    jcenter()
    maven {
        url "https://raw.github.com/synergian/wagon-git/releases"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    compile 'com.android.support:appcompat-v7:24.2.1'
    compile 'com.android.support:design:24.2.1'
    compile 'com.google.android.gms:play-services-vision:9.6.1'
    compile 'com.squareup.okhttp3:okhttp:3.2.0'
    compile 'io.card:android-sdk:5.1.2'
    deployerJars 'ar.com.synergian:wagon-git:0.2.5'
}

def getRepositoryUsername() {
    return System.properties['username']
}

def getRepositoryPassword() {
    return System.properties['password']
}

uploadArchives {
    repositories.mavenDeployer {
        configuration = configurations.deployerJars
        repository(url: REPO_URL) {
            authentication(userName: getRepositoryUsername(), password: getRepositoryPassword())
        }
        pom.groupId = GROUP
        pom.artifactId = POM_ARTIFACT_ID
        pom.version = sdkVersion
    }
}